type Eslint {
  """
  The source directory for the project.
  """
  pub source: Directory! @defaultPath(path: "/")

  """
  The base image to use.
  """
  pub baseImageAddress: String! = "node:25-alpine"

  """
  The package manager to use.
  """
  pub packageManager: String! = "npm"

  """
  Lint the source.
  """
  pub lint(extensions: [String!]! = [".js", ".jsx"]): Void @check {
    let ext = extensions.join(",")
    node(source, baseImageAddress, packageManager).base.
    withExec(["npx", "eslint", ".", "--ext", ext]).
    sync

    null
  }

  """
  Fix linting issues.
  """
  pub fix(extensions: [String!]! = [".js", ".jsx"]): Changeset {
    let ext = extensions.join(",")
    let extFilter = []
    for (e in extensions) {
      extFilter = extFilter + ["**/*" + e]
    }

    let fixed = node(source, baseImageAddress, packageManager).base.
    withExec(["npx", "eslint", ".", "--ext", ext, "--fix"]).
    directory(".").
    withoutDirectory("node_modules")

    let fixedJs = directory.
    withDirectory(".", fixed, include: extFilter)

    let sourceJs = directory.
    withDirectory(".", source.withoutDirectory("node_modules"), include: extFilter)

    fixedJs.
    changes(sourceJs)
  }
}
